/******
 * @author Yuki Suga, Tao Asato
 * @copyright Ogata-lab 2016
 * @lisence public domain
 *****/

#ifndef TrajectoryPlanner_idl
#define TrajectoryPlanner_idl

#include "BasicDataType.idl"
#include "ExtendedDataTypes.idl"
#include "InterfaceDataTypes.idl"

module Manipulation {

  typedef double Vector3[3];
  
  typedef double Matrix34[3][4];
  
  struct ObjectIdentifier {
    string name;
  };
  
  struct ObjectInfo {
    ObjectIdentifier objectID;
    RTC::Pose3D pose;
  };
  
  struct JointAngle {
    double data;
  };

  typedef sequence<JointAngle> JointAngleSeq;

  struct LimitValue {
    double upper;
    double lower;
  };

  enum JOINT_TYPE {
    JOINT_ROTATE,
    JOINT_SLIDE,
    JOINT_FIXED,
    JOINT_FREE,
    JOINT_UNKNOWN
  };
  
  struct JointParameter {
    string name;
    JOINT_TYPE jointType;
    Vector3 axis;
    Matrix34 offset;
    LimitValue limit;
  };

  struct RobotJointInfo {
    sequence<JointParameter> jointParameterSeq;
  };
  
  struct RobotIdentifier {
    string name;
  };
  
  struct ManipulationPlan {
    RobotIdentifier robotID;
    sequence<JointAngleSeq> manipPath;
  };

  struct CollisionPair {
    string name0;
    string name1;
  };

  typedef sequence<CollisionPair> CollisionPairSeq;
  
  struct MeshInfo {
    string name;
  };

  enum RETURN_ID {
    OK,
    MODEL_NOT_FOUND,
    INVALID_SETTING,
    INVALID_ARGUMENT,
    ERROR_UNKNOWN
  };
  
  struct ReturnValue {
    RETURN_ID id;
    string message;
  };

  struct EndEffectorPose {
    RTC::Pose3D pose;
  };
  
  
  interface ObjectDetectionService {
    ReturnValue detectObject(in ObjectIdentifier objectID, out ObjectInfo objInfo);
    ReturnValue setBaseFrame(in Matrix34 frame);
  };

  interface ObjectHandleStrategyService {
    ReturnValue getApproachOrientation(in ObjectInfo objInfo, out EndEffectorPose eePos);
  };

  interface KinematicSolverService {
    ReturnValue solveKinematics(in EndEffectorPose targetPose, in JointAngleSeq startJointAngles, out JointAngleSeq targetJointAngles);
  };
  
  /** This might be unused.
  interface CurrentStateService {
    ReturnValue getCurrentState(out RobotJointInfo robotJoint);
  };
  */
  
  interface CollisionDetectionService {
    ReturnValue isCollide(in RobotIdentifier robotID, in JointAngleSeq jointAngles, out CollisionPairSeq collisions);
  };
  
  interface ManipulationPlannerService {
    ReturnValue planManipulation(in RobotJointInfo jointsInfo, in JointAngleSeq startJointAngles, in JointAngleSeq goalJointAngles, out ManipulationPlan manipPlan);
  };
  
  interface ModelServerService {
    ReturnValue getModelInfo(in RobotIdentifier robotID, out RobotJointInfo jointsInfo);
    ReturnValue getMeshInfo(in RobotIdentifier robotID, out MeshInfo mesh);
  };
  
  interface MotionGeneratorService {
    ReturnValue followManipPlan(in ManipulationPlan manipPlan);
    ReturnValue getCurrentRobotJointAngles(out JointAngleSeq jointAngles);
  };
  
};

#endif
